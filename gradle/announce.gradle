apply plugin: "twitter-gradle-plugin"

def tag = System.getenv("CIRCLE_TAG")

if (tag) {
  def ghToken = System.getenv("GITHUB_OAUTH_TOKEN")
  def ghReleaseUrl = "https://api.github.com/repos/robfletcher/strikt/releases/tags/$tag?access_token=$ghToken"
  def releaseName = null
  try {
    releaseName = new groovy.json.JsonSlurper()
      .parseText(ghReleaseUrl.toURL().text)
      .name

    twitterPlugin {
      consumerKey = System.getenv("TWITTER_CONSUMER_KEY")
      consumerSecret = System.getenv("TWITTER_CONSUMER_SECRET")
      accessToken = System.getenv("TWITTER_ACCESS_TOKEN")
      accessTokenSecret = System.getenv("TWITTER_ACCESS_TOKEN_SECRET")
      message = "Strikt ${tag} ${releaseName} is available. https://strikt.io"
    }

    afterEvaluate {
      rootProject.tasks.postRelease.finalizedBy createTweet
    }
  } catch (IOException e) {
    logger.warn("Could not get release name from GitHub: $e.message")
  }
}
