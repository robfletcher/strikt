plugins {
  id "org.ajoberstar.github-pages" //version "1.7.2"
  id "org.jbake.site" version "1.2.0"
}

task copyApiDocs(type: Copy) {
  from(rootProject.tasks.findByPath(":strikt-core:dokka")) {
    include "**/*.html"
    filter(org.apache.tools.ant.filters.ConcatFilter, prepend: file("${projectDir}/api-header.txt"))
  }
  from(rootProject.tasks.findByPath(":strikt-protobuf:dokka")) {
    include "**/*.html"
    filter(org.apache.tools.ant.filters.ConcatFilter, prepend: file("${projectDir}/api-header.txt"))
  }
  from(rootProject.tasks.findByPath(":strikt-core:dokka")) {
    include "**/package-list"
  }
  from(rootProject.tasks.findByPath(":strikt-protobuf:dokka")) {
    include "**/package-list"
  }
  into("${projectDir}/src/jbake/content/api/")
  filter { line ->
    def strip = ["<HTML>", "<HEAD>", "<meta", "<title>", "<link", "</HEAD>", "<BODY>", "</BODY>", "</HTML>"]
    if (strip.any { line.startsWith(it) }) {
      null
    } else {
      line
    }
  }
}

configurations {
  jbake {
    resolutionStrategy.activateDependencyLocking()
  }
}

dependencies {
  jbake "com.orientechnologies:orientdb-core:2.2.34+"
}

jbake {
  flexmarkVersion = "0.34.28"
}

clean.doFirst {
  delete "${projectDir}/src/jbake/content/api/"
}

bake.dependsOn(copyApiDocs)

githubPages {
  repoUri = "git@github.com:robfletcher/strikt.git"
  targetBranch = "gh-pages"
  pages {
    from(bake) {
      exclude "**/*.html"
    }
    from(bake) {
      include "**/*.html"
      filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        version: (System.getenv('CIRCLE_TAG') ?: '+').replaceFirst(/^v/, '')
      ])
    }
    from(rootDir) {
      include ".circleci/**/*"
    }
  }
}
